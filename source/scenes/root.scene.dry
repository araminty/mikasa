title: Root Scene
new-page: true
go-to: post_event if started = 1; start_menu if started = 0

@start_menu
new-page: true
go-to: start_menu_2

= Mikasa Democracy: A Future History

@start_menu_2

- @start: Start game
- @election_simulation: Election simulation
- @credits: Credits
- @game_over.achievements


@start
on-arrival: {!
Q.started = 1;
// time elapsed (in months)
Q.time = 1;
Q.year = 2300;
Q.month = 1;
// how many actions have been taken this month?
Q.month_actions = 0;

// 0 = normal difficulty, 1 = hard mode, 2 = hard mode no saves
Q.difficulty = 0;
Q.use_decimals = 0;

Q.stacy_advisor = 1;

// Intercorporate
Q.soylent_magrail_rel = -50
Q.soylent_brazos_rel = 10
Q.magrail_brazos_rel = 20

// How much the corporations consider the government a problem
Q.soylent_anger = 0.05;
Q.magrail_anger = 0.05;
Q.brazos_anger = 0.05;

Q.martyrs = [];

// Suspicious towards outsiders but no problems... for now
Q.backlash = -.50;

// Syndicate policies
Q.government_families_rel = -75;
Q.government_edges_rel = 10;
Q.government_sharks_rel = 20;

Q.soylent_families_rel = 20;
Q.soylent_edges_rel = 0;
Q.soylent_sharks_rel = 0;

Q.magrail_families_rel = 10;
Q.magrail_edges_rel = 10;
Q.magrail_sharks_rel = 10;

Q.brazos_families_rel = 10;
Q.brazos_edges_rel = -50;
Q.brazos_sharks_rel = 50;

// percentage of people who think the control of the government would be worth anything
Q.government_respect = 0.05;
// percentage who aren't resigned to corporate control
Q.corp_backlash = 0.05;
// percentage who aren't resigned to organized crime
Q.crime_backlash = 0.05;

Q.xenophobia = .40;

!}
set-bg: img/map_2.jpg
new-page: true

> *"They named it Mikasa, 'Beauty'"*
-Kenji Fukushima, JAXA engineer, 2115

*"They named it Mikasa, 'Tragedy'"*
-Asani Ubuntu, Earth refugee, 2282



This is a game of future history. While historical accuracy is a major concern, scrying is an inexact science so large amounts of guesswork and interpolation were required.

- @2300_main: Begin (normal difficulty)


@2300_main
new-page: true
go-to: main if difficulty >= 0; main.main_easy if difficulty < 0

= 2300

It is a dark time for humanity.  Earth is mired in endless national conflicts while the off planet colonies are torn by violence between lawless corporations.

As the corporations of the interplanetary waystation Mikasa become too distracted fighting each other, real power has started to fall into the hands of the city government for the first time in decades.  Now our task is to somehow maintain this newfound autonomy while beating back corruption.  Perhaps Mikasa might one day control the corporations instead of the other way around.